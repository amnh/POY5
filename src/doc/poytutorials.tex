These tutorials are intended to provide guidance for more sophisticated applications of \poy that involve multiple steps and a combination of different commands. Each tutorial contains a \poy script that is followed by detailed commentaries explaining the rationale behind each step of the analysis. Although these analyses can be conducted interactively using the \emph{Interactive Console} or running separate sequential analyses using the \emph{Graphical User Interface}, the most practical way to do this is to use \poy scripts (see \emph{ POY5 Quick Start} for more information on \poy scripts).

It is important to remember that the numerical values for most command arguments will differ substantially depending on type, complexity, and size of the data. Therefore, the values used here should not be taken as optimal parameters.

The tutorials use sample datasets that are provided with \poy installation but can also be downloaded from the \poy site at
\begin{center}
\texttt{http://research.amnh.org/scicomp/projects/poy.php}
\end{center}
The minimal required items to run the tutorial analyses are the \poy application and the sample datafiles. Running these analyses requires some familiarity with \poy interface and command structure that can be found in the preceding chapters.

\section{Combining  search strategies}{\label{tutorial1}}
The following script implements a strategy for a thorough search. This is accomplished by generating a large number of independent initial trees by random addition sequence and combining different search strategies that aim at thoroughly exploring local tree space and escape the effect of composite optima by effectively traversing the tree space. In addition, this script shows how to output the status of the search to a log file and calculate the duration of the search. 

\begin{verbatim}
(* search using all data *)
read("9.fas","31.ss", aminoacids:("41.aa"))
(* We select as root the taxon with name t1. If we wanted
the taxon Locusta_migratoria, we would write:
root:"Locusta_migratoria" *)
set(seed:1,log:"all_data_search.log",root:"t1")
report(timer:"search start")
transform(tcm:(1,2),gap_opening:1)
build(250)
swap(threshold:5.0)
select()
perturb(transform(static_approx),iterations:15,ratchet:(0.2,3))
select()
fuse(iterations:200,swap())
select()
report("all_trees",trees:(total),"constree",graphconsensus,
"diagnosis",diagnosis)
report(timer:"search end")
set(nolog)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* search using all data *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read("9.fas","31.ss", aminoacids:("41.aa"))}
This command imports all the nucleotide sequence datafiles (all files with the extension \texttt{.seq}), a morphological datafile \texttt{morph.ss} in Hennig86 format, and an aminoacid datafile \texttt{myosin.aa}.
\item \texttt{set(seed:1,log:"all\_data\_search.log",root:"t1")} The \poycommand{set} command specifies conditions prior to tree searching. The \poyargument{seed} is used to ensure that the subsequent randomization procedures (such as tree building and selecting) are reproducible. Specifying the log produces a file, \texttt{all\_data\_search.log} that provides an additional means to monitor the process of the search. The outgroup (\texttt{taxon1}) is designated by the \poyargument{root}, so that all the reported trees have the desired polarity. By default, the analysis is performed using direct optimization.
\item \texttt{report(timer:"search start")} In combination with \texttt{report(timer:\\"search end")}, this commands reports the amount of time that the execution of commands enclosed by \poyargument{timer} takes. In this case, it reports how long it takes for the entire search to finish. Using timer is useful for planning a complex search strategy for large datasets that can take a long time to complete: it is instructive, for example, to know how long a search would last with a single replicate (one starting tree) before starting a search with multiple replicates.
\item \texttt{transform(tcm:(1,2),gap\_opening:1)} This command sets the \\transformation cost matrix for molecular data to be used in calculating the cost of the tree. Note, that in addition to the substitution and indel costs, the \poycommand{transform} specifies the cost for gap opening.
\item \texttt{build(250)} This commands begins tree-building step of the search that generates 250 random-addition trees. A large number of independent starting points insures that a large portion of tree space will be examined.
\item \texttt{swap(threshold:5.0)} \poycommand{swap} specifies that each of the 250 trees is subjected to alternating SPR and TBR branch swapping routine (the default of \poy). In addition to the most optimal trees, all the suboptimal trees found within 5\% of the best cost are thoroughly evaluated. This step ensures that the local searches settled on the local optima.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{perturb(transform(static\_approx),iterations:15,ratchet:\\(0.2,3))} This command subjects the resulting trees to 15 rounds of ratchet, re-weighting 20\% of characters by a factor of 2. During ratcheting, the dynamic homology characters are transformed into static homology characters, so that the fraction of nucleotides (rather than of sequence fragments) is being re-weighted. This step, that begins at multiple local maxima, is intended to further traverse the tree space in search of a global optimum.
\item \texttt{fuse(iterations:200,swap())} In this step, up to 200 swaps of subtrees identical in terminal composition but different in topology, are performed between pairs of best trees recovered in the previous step. This is another strategy for further exploration of tree space. Each resulting tree is further refined by local branch swapping under the default parameters of \poycommand{swap}.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory.
\item \texttt{report("all\_trees",trees:(total),"constree",\\graphconsensus,"diagnosis",diagnosis)} This command produces a series of outputs of the results of the search. It includes a file containing best trees in parenthetical notation and their costs (\texttt{all\_trees}), a graphical representation (in PDF format) of the strict consensus (\texttt{constree}), and the diagnoses for all best trees (\texttt{diagnosis}).
\item \texttt{report(timer:"search end")} This command stops timing the duration of search, initiated by the command \texttt{report(timer:"search start")}.
\item \texttt{set(nolog)} This command stops reporting any output to the log file, \texttt{all\_data\_search.log}.
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Searching under iterative pass}{\label{tutorial2}}
The following script implements a strategy for a thorough search under iterative pass optimization. The iterative pass optimization is a very time consuming procedure that makes it impractical to conduct under this kind of optimization (save for very small datasets that can be analyzed within reasonable time). The iterative pass, however, can be used for the most advanced stages of the analysis for the final refinement, when a potential global optimum has been reached through searches under other kinds of optimization (such as direct optimization). Therefore, this tutorial begins with importing an existing tree (rather than performing tree building from scratch) and subjecting it to local branch swapping under iterative pass.

\begin{verbatim}
(* search using all data under ip *)
read("9.fas","31.ss",aminoacids:("41.aa"))
read("inter_tree.tre")
transform(tcm:(1,2),gap_opening:1)
set(iterative:approximate:2)
swap(around)
select()
report("all_trees",trees:(total),"constree",graphconsensus,
"diagnosis",diagnosis)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* search using all data under ip *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read("9.fas","31.ss",aminoacids:("41.aa"))} This command imports all the nucleotide sequence datafiles (all files with the extension \texttt{.seq}), a morphological datafile \texttt{morph.ss} in Hennig86 format, and an aminoacid datafile \texttt{myosin.aa}.
\item \texttt{read("inter\_tree.tre")} This command imports a tree file, \texttt{inter\_tree.tre}, that contains the most optimal tree from prior analyses. 
\item \texttt{transform(tcm:(1,2),gap\_opening:1)} This command sets the transformation cost matrix for molecular data to be used in calculating the cost of the tree. Note, that in addition to the substitution and indel costs, the \poycommand{transform} specifies the cost for gap opening.
\item \texttt{set(iterative:approximate:2)} This command sets the optimization procedure
    to iterative pass such that approximated three dimensional alignments generated using pairwise alignments will be considered.  The program will iterate either two times, or until no further tress cost improvements can be made.
\item \texttt{swap(around)} This commands specifies that the the imported tree is subjected to alternating SPR and TBR branch swapping routine (the default of \poy) following the trajectory of search that completely evaluates the neighborhood of the tree (by using \poyargument{around}).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory.
\item \texttt{report("all\_trees",trees:(total),"constree",\\graphconsensus,"diagnosis",diagnosis)} This command produces a series of outputs of the results of the search. It includes a file containing best trees in parenthetical notation and their costs (\texttt{all\_trees}), a graphical representation (in PostScript format) of the strict consensus (\texttt{constree}), and the diagnoses for all best trees (\texttt{diagnosis}).
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Bremer support}{\label{tutorial 3}}

This tutorial builds on the previous tutorials to illustrate Bremer support 
calculation on trees constructed using dynamic homology characters
    
   \begin{verbatim}
(* Bremer support part 1: generating trees *)
read("18s.fas",28s.fas")
set(root:"Americhernus")
build(200)
swap(all,visited:"tmp.trees", timeout:3600)
select()
report("my.tree",trees)
exit()

(* Bremer support part 2: Bremer calculations *)
read("18s.fas",28s.fas","my.tree")
report("support_tree.pdf",graphsupports:bremer:"tmp.trees")
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Bremer support part1: generating trees *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes. 
\item \texttt{read("18s.fas","28s.fas")} This command imports the nucleotide sequence files \texttt{18s.fas, 28s.fas}.
\item \texttt{set(root:"Americhernus")} The \poycommand{set} command specifies conditions prior to tree searching. The outgroup (\texttt{Americhernus}) is designated by the \poyargument{root}, so that all the reported trees have the desired polarity.     
\item \texttt{build(200)} This commands initializes tree-building and generates 200 random-addition trees.      
\item \texttt{swap(all,visited:"tmp.trees", timeout:3600)} The \poycommand{swap} command specifies that each of the trees be subjected to an alternating SPR and TBR branch swapping routine (the default of \poy).  The \poyargument{all} argument turns off all swap heuristics. The \poyargument{visited:"tmp.trees"} argument stores every visited tree in the file specified.  Although the visited tree file is compressed to accommodate the large number of trees it will accumulate, the argument \poyargument{timeout} can be used to limit the number of seconds allowed for swapping also limiting the size of the file.  Alternately  the  \poycommand{swap} command can be performed as a separate analysis and terminated at the users discretion to maximize the number of trees generated.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{report("my.tree",trees)} This command will save the swapped tree, \\ \texttt{my.tree} to a file. 
\item \texttt{exit()} This commands ends the \poy session.

\item \texttt{(* Bremer support part 2: Bremer calculations *)}  A comment indicating the intent of the commands which follow.
\item \texttt{read("18s.fas","28s.fas","my.tree")} This command imports the nucleotide sequence files \texttt{18s.fas, 28s.fas} and the tree file, \texttt{my.tree} for which the support values will be generated.  It is important to only read the selected \texttt{"my.tree"} file rather than the expansive  \texttt{"tmp.trees"} file which will be used in bremer calculations.
\item \texttt{report("support\_tree.pdf",graphsupports:bremer:"tmp.trees")} \\The \poycommand{report} command in combination with a file name and the \\ \poyargument{graphsupports} generates a pdf file designated by the name \texttt{support\_tree.pdf} with bremer values for the selected trees held in \texttt{tmp.trees}.  It is strongly recommended that this more exhaustive approach is used for calculating Bremer supports rather than simply using the \\ \poyargument{graphsupports} defaults.  
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Jackknife support}{\label{tutorial 4}}

This tutorial illustrates calculating Jackknife support values for trees constructed with static homology characters.  Although it is possible to calculate Jackknife and Bootstrap support values 
for trees constructed using dynamic homology characters, it is not recommended because resampling of dynamic characters occurs at the fragment (rather than nucleotide) level. Alternately dynamic homology characters can be converted to static characters using the transform argument \poyargument{static\_approx}.  Character transformation, however, may result in a discrepancy between tree costs generated using dyanmic homology characters and those generated using static homology characters. Therefore after such a data transformation is performed, an extra round of swapping is recommended to insure that the local minimum tree length is reached for the static homology characters prior to calculating support values  support values.

\begin{verbatim}
(* Jackknife support for static homology trees *)
read(prealigned:("28s.aln",tcm:(1,2)))
set( root:"Americhernus")
build()
swap()
select()
calculate_support(jackknife:(remove:0.50,resample:1000), 
build(5),swap(tbr,trees:3))
report("jacktree",graphsupports)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Jackknife support for static homology trees *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read(prealigned:("28s.aln",tcm:(1,2)))} This command imports the prealigned nucleotide sequence file \texttt{28s.aln}, and treats the characters as static with the prescribed transformation cost matrix.
\item \texttt{set(root:"Americhernus")} The \poycommand{set} command specifies conditions prior to tree searching. The outgroup (\texttt{Americhernus}) is designated by the \poyargument{root}, so that all the reported trees have the desired polarity.     
\item \texttt{build()} This commands begins the tree-building step of the search that generates by default 10 random-addition trees. It is essential that trees are either specified from a file or that trees are built and loaded in memory before attempting to calculate support values.
\item \texttt{swap()} The \poycommand{swap} command specifies that each of the trees be subjected to an alternating SPR and TBR branch swapping routine (the default of \poy).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{calculate\_support(jackknife,(remove:0.50,resample:1000)} The \poycommand{calculate\_support} command generates support values as specified by the \poyargument{jackknife} argument for each tree held in memory. During each pseudoreplicate half of the characters will be deleted as specified in the argument\poyargument{remove:0.50}. 
\item \texttt{report("jacktree",graphsupports:jackknife)}  The \poycommand{report} command in combination with a file name and the \poyargument{graphsupports} generates a pdf file with jackknife values designated by the name specified (\emph{i.e.} \texttt{jacktree}). 
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Sensitivity analysis}{\label{tutorial 5}}

This tutorial demonstrates how data for parameter sensitivity analysis is generated. Sensitivity analysis \cite{wheeler1995} is a method of exploring the effect of relative costs of substitutions (transitions and transversions) and indels (insertions and deletions), either with or without taking gap extension cost into account. The approach consists of multiple iterations of the same search strategy under different parameters, (\emph{i.e} combinations of substitution and indel costs. Obviously, such analysis might become time consuming and certain methods are shown here how to achieve the results in reasonable time. This tutorial also shows the utility of the command \poycommand{store} and how transformation cost matrixes are imported and used.

\poy does not comprehensively display the results of the sensitivity analysis or implements the methods to select a parameter set that produces the optimal cladogram, but the output of a \poy analysis (such as the one presented here) generates all the necessary data for these additional steps.

For the sake of simplicity, this script contains commands for generating the data under just two parameter  sets. Using a larger number of parameter sets can easily be achieved by replicating the repeated parts of the script and substituting the names of input cost matrixes.

\begin{verbatim}
(* sensitivity analysis *)
read("9.fas")
set(root:"t1")
store("original_data")
transform(tcm:"111.txt")
build(100)
swap(timeout:3600)
select()
report("111.tre",trees:(total) ,"111con.tre",consensus,
"111con.pdf",graphconsensus)
use("original_data")
transform(tcm:"112.txt")
build(100)
swap(timeout:3600)
select()
report("112.tre",trees:(total),"112con.tre",consensus,
"112con.pdf",graphconsensus)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* sensitivity analysis *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read("9.fas")} This command imports all dynamic homology nucleotide data.
\item \texttt{set(root:"t1")} The outgroup (\texttt{taxon1}) is designated by the \poyargument{root}, so that all the reported trees have the desired polarity.
\item \texttt{store("original\_data")} This commands stores the current state of analysis in memory in a temporary file, \texttt{original\_data}.
\item \texttt{transform(tcm:"111.txt")} This command applies a transformation cost matrix from the file \texttt{111.txt} to for subsequent tree searching.
\item \texttt{build(100)} This commands begins tree-building step of the search that generates 250 random-addition trees. A large number of independent starting point insures that thee large portion of tree space have been examined.
\item \texttt{swap(timeout:3600)} \poycommand{swap} specifies that each of the 100 trees build in the previous step is subjected to alternating SPR and TBR branch swapping routine (the default of \poy). The argument \poyargument{timeout} specifies that 3600 seconds are allocated for swapping and the search is going to be stopped after reaching this limit. Because sensitivity analysis consists of multiple independent searches, it can take a tremendous amount of time to complete each one of them. In this example, \poyargument{timeout} is used to prevent the searches from running too long. Using \poyargument{timeout} is optional and can obviously produce suboptimal results due to insufficient time allocated to searching. A reasonable timeout value can be experimentally obtained by the analysis under one cost regime and monitoring time it takes to complete the search (using the argument \poyargument{timer} of the command \poycommand{set}). The advantage of using \poyargument{timeout} is saving time in cases where a local optimum is quickly reached and the search is trapped in its neighborhood.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory.
\item \texttt{report("111.tre",trees:(total) ,"111con.tre",consensus,\\"111con.pdf", graphconsensus)} This command produces a file containing best tree(s) in parenthetical notation and their costs (\texttt{111.tre}), a a file containing the strict consensus in parenthetical notation \\(\texttt{111con.tre}), and a graphical representation (in PDF format) of the strict consensus (\texttt{111con.pdf}).
\item \texttt{use("original\_data")} This command restored the original (non-trans\-formed) data from the temporary file \texttt{original\_data} generated by \poycommand{store}.
\item \texttt{transform(tcm:"112.txt")} This command applies a different transformation cost matrix from the file \texttt{112.txt} to for another round of tree searching under this new cost regime.
\item \texttt{build(100)} This commands begins tree-building step of the search that generates 100 random-addition trees. A large number of independent starting point insures that thee large portion of tree space have been examined.
\item \texttt{swap(timeout:3600)} \poycommand{swap} specifies that each of the 100 trees build in the previous step is subjected to alternating SPR and TBR branch swapping routine (the default of \poy) to be interrupted after 3600 seconds (see the description in the previous iteration of the command above).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory.
\item \texttt{report("112.tre",trees:(total),"112con.tre",consensus,\\"112con.pdf", graphconsensus)} This command produces a set of the same kinds of outputs as generated during the first search (see above) but under a new cost regime.
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Chromosome analysis: unannotated sequences}{\label{tutorial 6}}

This tutorial illustrates the analysis of chromosome-level transformations using 
unannotated sequences, i.e., contiguous strings of sequences without prior 
identification of independent regions. 

\begin{verbatim}
(* Chromosome analysis of unannotated sequences *)
read(chromosome:("mit5.txt"))
transform(chromosome:(locus_inversion:15,locus_indel:(10,0.9)))
transform(chromosome:(annotate:(mauve,25,.3,0.01,.08)))
build()
swap()
select()
report("chrom",diagnosis)
report("consensustree",graphconsensus)
transform(fixed_states:("mauveout",ignore_polymorphism))
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Chromosome analysis of unannotated sequences *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read(chromosome:("mit5.txt"))} This command imports the unannotated chromosomal sequence file \texttt{mit5.txt}. The argument \poyargument{chromosome} specifies the characters as unannotated chromosomes.
\item \texttt{transform(chromosome:(locus\_inversion:15,locus\_indel:\\(10,0.9))}.  The \poycommand{trans\-form} followed by the argument \poyargument{chromosome} signifies the conditions to be applied when calculating chromosome-level (medians).  The argument \poyargument{locus\_inversion:15} applies a inversion distance between chromosome loci with the integer value determining the rearrangement cost. The argument \poyargument{locus\_indel:10,0.9} specifies the indel costs for the chromosomal segments, whereby the integer 10 sets the gap opening cost and the float 0.9 sets the gap extension cost.
\item \texttt{transform(chromosome:(annotate:(mauve,25,.3,0.01,.08)))} The argument \poyargument{annotate:(mauve)} specifies that the program will use the Mauve aligner \cite{darlingetal2004} to determine locally collinear homologous blocks within the chromosomal sequences.  The values that follow the Mauve option set the parameters for determining the lcb homologies: quality, coverage, and minimum and maximum lcb length relative to overall sequence length. In this case the lcb quality parameter which represents the cost of the lcb divided by its length of lcb is set to the relatively low value of 25 to facilitate the detection of blocks within the sequences.  The higher the lcb quality values will result in more stringent lcb determination and likely fewer local collinear blocks recovered.  The second parameter withn the argument \poyargument{annotate:(mauve)} sets the minimum lcb sequence coverage at 30\% meaning that if total length of an input sequence is, for example,100, a minimum coverage of .30 would require a the total length of all lcbs to be at least 30. The default value of .01 or 1\% is sets the minimum length of a given lcb relative to the length of the entire sequence (e.g. 100 for a 10,000 nucleotide sequence). The maximum length allowed for an lcb in this example is set at 8\% of the length of the total sequence.
\item \texttt{build()} This commands begins the tree-building step of the search that generates by default 10 random-addition trees. It is highly recommended that a greater number of Wagner builds be implemented when analyzing data for purposes other than this demonstration.
\item \texttt{swap()} The \poycommand{swap} command specifies that each of the trees be subjected to an alternating SPR and TBR branch swapping routine (the default of \poy).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{report("chrom",diagnosis)}  The \poycommand{report} command in combination with a file name and the \poyargument{diagnosis} outputs the optimal median states and edge values to a specified file (\texttt{chrom}). 
\item \texttt{report("consensustree",graphconsensus)}  The \poycommand{report} command in combination with a file name and the \poyargument{graphconsensus} generates a pdf strict consensus file of the trees generated (\texttt{consensustree}). 
\item \texttt{transform(fixed\_states:("mauveout", ignore\_polymorphism))}  The \poycommand{transform} command in combination with \poyargument{fixed\_states:("mauveout",ignore\_polymorphism))} is used to produce output that can be read into Mauve to generate a pairwise alignment tracking the movement of lcbs between sequences. Sequence ambiguities will not be resolved to generate additional medians beyond those determined by the data.
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Chromosome analysis: annotated sequences}{\label{tutorial 7}}

This tutorial illustrates the analysis of chromosome-level transformations using 
annotated sequences, i.e., contiguous strings of sequences with prior 
identification of independent regions delineated by pipes  \texttt{"|"}. 

\begin{verbatim}
(* Chromosome analysis of annotated sequences *)
read(annotated:("aninv2"))
transform(chromosome:(locus_inversion:20,locus_indel:(10,
1.5),circular:false,median:1,swap_med:1)))
build(100)
swap()
select()
report("Annotated",diagnosis)
report("consensustree",graphconsensus)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Chromosome analysis of annotated sequences  *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read(annotated:("aninv2"))} This command imports the annotated chromosomal sequence file \texttt{aninv2}. The argument \poyargument{annotated} specifies the characters. 
\item \texttt{transform((chromosome:(locus\_inversion:20,locus\_indel:\\(10,1.5),median:1,swap\_med:1)))}  The \poycommand{transform} follow\-ed by the argument \poyargument{chromosome} specifies the conditions to be applied when calculating chromosome-level (medians).  The argument \poyargument{locus\_inversion:20} applies an inversion distance between chromosome loci with the integer value determining the rearrangement cost and using the default caprara median solver. The argument \poyargument{locus\_indel:\\(10,1.5} specifies the indel costs for chromosomal segments, where the integer 10 sets the gap opening cost and the float 1.5 sets the gap extension cost.  The default values are applied to the arguments \poyargument{circular}  \poyargument{median} and \poyargument{swap\_med} arguments to minimize the time require for these nested search options.   To more exhaustively perform these calculations, trees generated from initial builds can be imported to the program and reevaluated with values greater than 1 entered for the \poyargument{median} and \poyargument{swap\_med} arguments.
\item \texttt{build()} This commands begins the tree-building step of the search that generates by 100 random-addition trees.  It is highly recommended that a greater number of Wagner builds be implemented when analyzing data for purposes other than this demonstration.
\item \texttt{swap()} The \poycommand{swap} command specifies that each of the trees be subjected to an alternating SPR and TBR branch swapping routine (the default of \poy).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{report("Annotated",diagnosis)}  The \poycommand{report} command in combination with a file name and the \poyargument{diagnosis} outputs the optimal median states and edge values to a specified file (\texttt{Annotated}). 
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Custom alphabet and break inversion characters}{\label{tutorial 8}}

This tutorial illustrates the analysis of the break inversion character type.  Break inversion characters are generated by transforming user-defined \poyargument {custom\_alphabet} characters.  
For example, observations of developmental stages could be represented in a corresponding array such that for each terminal taxon there is a sequence of observed developmental stages which are represented by a user-defined alphabet.  To allow rearrangement as well as indel events to be considered among alphabet elements, requires transforming the \poyargument {custom\_alphabet} sequences to \poyargument {breakinv} characters. 

\begin{verbatim}
(* Custom Alphabet to Breakinv characters *)
read(custom_alphabet:("ca1.fas","m1.fas"))
transform(custom_to_breakinv:()))
transform(breakinv:(median_solver: siepel(locus_inversion:20, locus_indel:(10,1.5),median:1,swap_med:1)))
build()
swap()
select()
report("breakinv",diagnosis)
report("consensustree",graphconsensus)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Custom Alphabet to Breakinv characters  *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read(custom\_alphabet:("ca1.fas","m1.fas"))} This command imports the user-defined \poyargument {custom\_alphabet} character file \texttt{ca1.fas} and the accompanying transformation matrix \texttt{m1.fas}.
\item \texttt{transform(custom\_to\_breakinv)} This command transforms \poyargument {custom\_alphabet} characters to \poyargument {breakinv} characters which allow for rearrangement operations.
\item \texttt{transform(breakinv:(median\_solver: siepel(locus\_inversion:20,locus\_indel:(10,1.5),median:1,swap\_med:1)))}  The \poycommand{transform} followed by the argument \poyargument{breakinv} specifies the conditions to be applied when calculating medians. The argument \poyargument{[median\_solver:siepel} specifies that the Siepel median from the GRAPPA software package \cite{baderetal2002} will be employed.  The argument \poyargument{locus \_inversion:20} applies an inversion rearrangement cost of 20 for \poyargument {breakinv} elements. The argument \poyargument{locus\_indel:10,1.5} specifies the indel costs for each \poyargument {breakinv} element, whereby the integer 10 sets the gap opening cost and the float 1.5 sets the gap extension cost.  The default values are applied to the \poyargument{median} and \poyargument{swap\_med} arguments to minimize the time require for these nested search options.   To more exhaustively perform these calculations trees generated from initial builds can be imported to the program and reevaluated with values greater than 1 designated for the \poyargument{median} and \poyargument{swap\_med} arguments
\item \texttt{build()} This commands begins the tree-building step of the search that generates by default 10 random-addition trees.  It is highly recommended that a greater number of Wagner builds be implemented when analyzing data for purposes other than this demonstration.
\item \texttt{swap()} The \poycommand{swap} command specifies that each of the trees be subjected to an alternating SPR and TBR branch swapping routine (the default of \poy).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{report ("breakinv",diagnosis)}  The \poycommand{report} command in combination with a file name and the \poyargument{diagnosis} outputs the optimal median states and edge values to a specified file (\texttt{breakinv}). 
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Genome analysis: multiple chromosomes}{\label{tutorial 9}}

This tutorial illustrates the analysis of genome-level transformations using data from multiple chromosomes. 

\begin{verbatim}
(* Genome analysis of multiple chromosomes *)
read (genome:("gen5bp"))
transform(genome:(translocation:50))
transform(chromosome:(annotate:(mauve,25,.3,0.01,.08)))
transform(chromosome:(locus_breakpoint:80,locus_indel:
(15,2.5),median:1,swap_med:1))
build()
swap()
select()
report("genome",diagnosis)
report("genconsensus",graphconsensus)
exit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Genome analysis of multiple chromosomes*)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they provide are useful for housekeeping purposes.
\item \texttt{read(genome:("gen5bp"))} This command imports the genomic sequence file \texttt{mit5.txt}. The argument \poyargument{genome} specifies the characters as data consisting of multiple chromomsomes.
\item \texttt{transform(genome:(translocation:50))} sets the breakpoint cost for the movement of lcbs from one chromosomal segment to another. 
\item \texttt{transform(chromosome:(annotate:(mauve,25,.3,0.01,.08)))} The argument \poyargument{annotate:(mauve)} specifies that the program will use the Mauve aligner \cite{darlingetal2004} to determine locally collinear homologous blocks within the chromosomal sequences.  The values that follow the Mauve option set the parameters for determining the lcb homologies: quality, coverage, and minimum and maximum lcb length relative to overall sequence length. In this case the lcb quality parameter which represents the cost of the lcb divided by its length of lcb is set to the relatively low value of 25 to facilitate the detection of blocks within the sequences.  The higher the lcb quality values will result in more stringent lcb determination and likely fewer local collinear blocks recovered.  The second parameter withn the argument \poyargument{annotate:(mauve)} sets the minimum lcb sequence coverage at 30\% meaning that if total length of an input sequence is, for example,100, a minimum coverage of .30 would require a the total length of all lcbs to be at least 30. The default value of .01 or 1\% is sets the minimum length of a given lcb relative to the length of the entire sequence (e.g. 100 for a 10,000 nucleotide sequence). The maximum length allowed for an lcb in this example is set at 8\% of the length of the total sequence.
\item \texttt{transform(chromosome:(locus\_breakpoint:20,locus\_indel:(10,1.5),\\median:1,swap\_med:1)))}  The \poycommand{transform} followed by the argument \poyargument{chromosome} specifies the conditions to be applied when calculating genome-level HTUs (medians). The argument \poyargument{chrom\_breakpoint:80} applies a breakpoint distance between chromosomes with the integer value determining the rearrangement cost. The argument \poyargument{chrom\_indel:15,1.5} specifies the indel costs for each entire chromosome, whereby the integer sets the gap opening cost and the float sets the gap extension cost.  The argument \poyargument{inversion:20} applies an inversion distance between chromosome loci with the integer value determining the rearrangement cost. The argument \poyargument{locus\_indel:10,1.5} specifies the indel costs for the chromosomal segments, whereby the integer 10 sets the gap opening cost and the float 1.5 sets the gap extension cost.  The default values are applied to the \poyargument{median} and \poyargument{swap\_med} arguments to minimize the time require for these nested search options.   To more exhaustively perform these calculations trees generated from initial builds can be imported to the program and reevaluated with values greater than 1 designated for the \poyargument{median} and \poyargument{swap\_med} arguments
\item \texttt{build()} This commands begins the tree-building step of the search that generates by default 10 random-addition trees.  It is highly recommended that a greater number of Wagner builds be implemented when analyzing data for purposes other than this demonstration.
\item \texttt{swap()} The \poycommand{swap} command specifies that each of the trees be subjected to an alternating SPR and TBR branch swapping routine (the default of \poy).
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{report("genome",diagnosis)}  The \poycommand{report} command in combination with a file name and the \poyargument{diagnosis} outputs the optimal median states and edge values to a specified file (\texttt{genome}). 
\item \texttt{report("genconsens",graphconsensus)}  The \poycommand{report} command in combination with a file name and the \poyargument{graphconsensus} generates a pdf strict consensus file of the trees generated (\texttt{genconsensus}). 
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Traditional maximum likelihood analysis with static characters}{\label{tutorial10}}
The following script covers the basics required for an analysis under the maximum likelihood criterion that is of similar intensity to that of a search using the GTR model in PhyML. Because there are many steps in, and options for, maximum likelihood in POY, each step is explained twice, once as a comment within the script itself, and once in the itemized tutorial body, below. Because searches conducted using maximum likelihood require a significant computational burden, parsimony alternatives to RAS builds under likelihood are provided. Additionally, throughout the tutorial, some familiarity with the terminology of likelihood analyses is assumed.

\begin{verbatim}
(* Read in the data and specify static characters *)
read(prealigned:("9.fas",tcm:(1,0)))

(* Conduct a parsimony search to generate a pool of trees to 
   transform to likelihood *)
build(100)
swap()
select()
perturb(transform(static_approx),iterations:50,ratchet:(0.2,3))
fuse(iterations:200,swap())
select()

(* Transform the static data to likelihood characters, specifying a 
    GTR + Gamma 4 model with empirical equilibrium frequencies under 
    traditional MAL *)
transform(likelihood:(gtr,rates:(gamma:(4)),priors:(estimate),gap:(missing),mal))
swap(spr,iteration:(model:(max_count:5),branch:join_region))
select(best:1)
report("9likelihood.tre",trees:(branches))
report("9likelihood.lkm",lkmodel)
wipe()
quit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Read in the data and specify static characters *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for housekeeping purposes.
\item \texttt{read(prealigned:("9.fas",tcm:(1,0)))}
This command imports the nucleotide sequence datafile \texttt{9.fas} as prealigned characters. Note that the \poycommand{tcm} specified for this import is non-metric, but consistent with the assumption of indels as ``missing'' data. This tcm is included for consistency, as later likelihood swaps make equivalent assumptions.
\item \texttt{build(100)} This command generates 100 random-addition Wagner trees. A large number of independent starting points ensures that a large portion of tree space have been examined.
\item \texttt{swap()} \poycommand{swap} specifies that each of the 100 trees is subjected to alternating SPR and TBR branch swapping routines (the default of \poy). All trees with the optimal score found are stored in memory.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{perturb(transform(static\_approx),iterations:50,ratchet:\\(0.2,3))} This command subjects the resulting trees to 50 rounds of parsimony ratchet, re-weighting 20\% of characters by a factor of 3.
\item \texttt{fuse(iterations:200,swap())} In this step, up to 200 swappings of subtrees identical in terminal composition but different in topology, are performed between pairs of best trees recovered in the previous step. This is another strategy for further exploration of tree space. Each resulting tree is further refined by local branch swapping under the default parameters of \poycommand{swap}.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory.
\item \texttt{transform(likelihood:(gtr,rates:(gamma:(4)),priors:(estimate),gap:(missing),mal))} This command transforms the characters to static likelihood characters, using a GTR + $\Gamma 4$ model, with empirical equilibrium frequencies under standard MAL. In this model, indels are treated as ``missing'' data, as for the preceding parsimony search. Several default values, such as \texttt{gap:(missing)}, and \texttt{gamma:(4)} are listed explicitly.
\item \texttt{swap(spr,iteration:(model:(max\_count:5),branch:join\_region))} This command swaps the tree using subtree pruning and regrafting, specifying that iteration of likelihood model parameters will occur after every 5 joins, and that iteration of branches will occur only in the area around the join point. These iteration specifications are less strict than the defaults for the purpose of speeding up the swapping.
\item \texttt{select(best:1)} This command saves the single most optimal topology (with branch lengths) in memory. Other trees are purged.
\item \texttt{report("9likelihood.tre",trees:(branches))} This command outputs the topology, with branch lengths, in \texttt{.tre} format.
\item \texttt{report("9likelihood.lkm",lkmodel)} This command outputs the result of the likelihood analysis, which consists of the likelihood score, the variant of likelihood used, the tree length (sum of branch lengths), the values of the parameter estimates for the entries of the \textbf{P} and \textbf{Q} matrices, and the estimate of the value of the rate variation shape parameter.
\item \texttt{wipe()} This commands clears the memory.
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}

\section{Dynamic maximum likelihood analysis}{\label{tutorial11}}
The following script covers an analysis using dynamic MPL, and assumes you have covered the previous tutorial, ``Traditional maximum likelihood analysis using static characters'' and are familiar with the intensity of the search strategy employed therein. As with the previous tutorial, some familiarity with the terminology of likelihood analyses is assumed.

\begin{verbatim}
(* Read in the data. Do NOT specify static characters *)
read("9.fas")

(* Conduct a parsimony direct optimization search to generate a
   pool of trees to transform to likelihood *)
build(100)
swap()
select()
perturb(transform(static_approx),iterations:50,ratchet:(0.2,3))
fuse(iterations:200,swap())
select()

(* Transform the parsimony DO characters to dynamic MPL
   characters, specifying a GTR  model with empirical equilibrium
   frequencies and a shared nucleotide-indel substitution rate. *)
transform(likelihood:(gtr,priors:(estimate),gap:(coupled),mpl))
swap(spr,iteration:(model:(max_count:5)))
select(best:1)
report("9likelihood.ia",ia:all)
report("9likelihoodYtree.tre",trees:(branches))
report("9likelihoodYmodel.lkm",lkmodel)
wipe()

(* Re-load the alignment/tree pair generated by dynamic MPL and
   re-diagnose as static MAL characters *)
read(prealigned:("9likelihood.ia",tcm:(1,0)),"9likelihoodYtree.tre")
transform(likelihood:(gtr,rates:(gamma:(4)),priors:(estimate)))
report("9likelihoodYdynamic_as_static.lkm",lkmodel)
wipe()

(* Re-load the implied alignment as a heuristic MSA and conduct
   a quick search under parsimony *)
read(prealigned:("9likelihood.ia",tcm:(1,0)))
build(20)
swap()
select()
perturb(transform(static_approx),iterations:50,ratchet:(0.2,3))
select()

(* Transform the static characters to static MPL characters and swap *)
transform(likelihood:(gtr,gap:(coupled),mpl)))
swap(spr,iteration:(model:(max_count:5)))
report("9likelihoodYdynamicMPL_as_staticMPL.lkm",lkmodel)
report("9likelihoodYdynamic_as_static.tre",trees:(branches))
wipe()

quit()
\end{verbatim}

\begin{itemize}
\item \texttt{(* Read in the data. Do NOT specify static characters. *)} This first line of the script is a comment. While comments are optional and do not affect the analyses, they are useful for separating different components of an analysis, especially if the script is long.
\item \texttt{read("9.fas")}
This command imports the nucleotide sequence datafile \texttt{9.fas} under the default behavior of treatment as dynamic characters.
\item \texttt{build(100)} This command generates 100 random-addition Wagner trees. A large number of independent starting points ensures that a large portion of tree space have been examined.
\item \texttt{swap()} \poycommand{swap} specifies that each of the 100 trees is subjected to alternating SPR and TBR branch swapping routines (the default of \poy). All trees with the optimal score found are stored in memory.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{perturb(transform(static\_approx),iterations:50,ratchet:\\(0.2,3))} This command subjects the resulting trees to 50 rounds of parsimony ratchet, re-weighting 20\% of characters by a factor of 3.
\item \texttt{fuse(iterations:200,swap())} In this step, up to 200 swappings of subtrees identical in terminal composition but different in topology, are performed between pairs of best trees recovered in the previous step. This is another strategy for further exploration of tree space. Each resulting tree is further refined by local branch swapping under the default parameters of \poycommand{swap}.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory.
\item \texttt{transform(likelihood:(gtr,priors:(estimate),gap:(coupled),mpl))} This command transforms the characters to static likelihood characters, using a GTR model, with empirical equilibrium frequencies under dynamic MPL. Note that under dynamic MPL, the rate variation distribution is not enabled.
\item \texttt{swap(spr,iteration:(model:(max\_count:5)))} This command swaps the tree using subtree pruning and regrafting, specifying that iteration of likelihood model parameters will occur after every 5 joins. These iteration specifications are less strict than the defaults for the purpose of speeding up the swapping.
\item \texttt{select(best:1)} This command saves the single most optimal topology (with branch lengths) in memory. Other trees are purged.
\item \texttt{report("9likelihood.ia",ia:all)} This command saves the implied
    alignment generated by dynamic MPL as a file named \texttt{9likelihood.ia}.
\item \texttt{report("9likelihoodYtree.tre",trees:(branches))} This command saves the topology in memory with branch lengths as a \texttt{.tre} file named \texttt{9likelihoodYtree}.
\item \texttt{report("9likelihoodYmodel.lkm",lkmodel)} This command saves the parameter estimates generated by dynamic MPL as a file named \texttt{9likelihoodYmodel.lkm}.
\item \texttt{wipe()} This commands clears the memory.
\item \texttt{read(prealigned:("9likelihood.ia",tcm:(1,0)),"9likelihoodYtree.tre")} Read in the tree/alignment combination produced by the preceding search under dynamic MPL as static characters.
\item \texttt{transform(likelihood:(gtr,rates:(gamma:(4)),priors:(estimate)))} Transform the static characters to MAL, using a GTR + $\Gamma 4$ model and empirical equilibrium frequencies. POY will re-diagnose the topology using this model and optimize the values.
\item \texttt{report("9likelihoodYdynamic\_as\_static.lkm",lkmodel)} Report the output of the likelihood analysis to the file \texttt{9likelihoodYdynamic\_as\_static.lkm}.
\item \texttt{wipe()} This commands clears the memory.
\item \texttt{read(prealigned:("9likelihood.ia",tcm:(1,0)))} This command
    imports the nucleotide implied alignment \texttt{9likelihood.ia} as prealigned characters. Note that the \poycommand{tcm} specified for this import is non-metric, but consistent with the assumption of indels as ``missing'' data. This tcm is included for consistency, as later likelihood swaps make equivalent assumptions.
\item \texttt{build(20)} This command generates 20 random-addition Wagner trees. A large number of independent starting points ensures that a large portion of tree space have been examined.
\item \texttt{swap()} \poycommand{swap} specifies that each of the 20 trees is subjected to alternating SPR and TBR branch swapping routines (the default of \poy). All trees with the optimal score found are stored in memory.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{perturb(transform(static\_approx),iterations:50,ratchet:\\(0.2,3))} This command subjects the resulting trees to 50 rounds of parsimony ratchet, re-weighting 20\% of characters by a factor of 3.
\item \texttt{select()} Upon completion of branch swapping, this command retains only optimal and topologically unique trees; all other trees are discarded from memory. 
\item \texttt{transform(likelihood:(gtr,gap:(coupled),mpl)))} Transform the static parsimony characters to static MPL characters with a GTR model, empirical equilibrium frequencies, and a coupled nucleotide-indel parameter.
\item \texttt{swap(spr,iteration:(model:(max\_count:5)))} This command swaps the tree using subtree pruning and regrafting, specifying that iteration of likelihood model parameters will occur after every 5 joins. These iteration specifications are less strict than the defaults for the purpose of speeding up the swapping.
\item \texttt{report(""9likelihoodYdynamicMPL\_as\_staticMPL.lkm",lkmodel)} This command outputs the result of the likelihood analysis to the file \texttt{"9likelihoodYdynamicMPL\_as\_staticMPL.lkm}.
\item \texttt{report("9likelihoodYdynamic\_as\_static.tre",trees:(branches))} This command outputs the topology, with branch lengths, in \texttt{.tre} format.
\item \texttt{wipe()} This commands clears the memory.
\item \texttt{exit()} This commands ends the \poy session.
\end{itemize}
